buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
    }
}

plugins {
    id 'idea'
    id 'maven'
    id 'groovy'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.10.1'
    id "com.diffplug.gradle.spotless" version '3.25.0'
    id "de.undercouch.download" version '4.0.0'
}

apply {
    [
        'jacoco',
        'java',
        'javadoc',
        'repositories',
    ].each { buildScript ->
        download {
            src "https://raw.githubusercontent.com/web3j/build-tools/master/gradle/$buildScript/build.gradle"
            dest "$rootDir/gradle/$buildScript/build.gradle"
            overwrite true
            quiet true
            onlyIfModified true
        }
        from("$rootDir/gradle/$buildScript/build.gradle")
    }
}

description = 'Solidity Gradle Plugin'

gradlePlugin {
    plugins {
        solidity {
            id = 'org.web3j.solidity'
            implementationClass = 'org.web3j.solidity.gradle.plugin.SolidityPlugin'
        }
    }
}

pluginBundle {
    website = 'https://web3j.io/'
    vcsUrl = 'https://github.com/web3j/solidity-gradle-plugin'
    description = 'Gradle plugin providing tasks to compile Solidity contracts.'
    tags = ['solidity', 'ethereum']
    plugins {
        solidity {
            // id is captured from java-gradle-plugin configuration
            displayName = project.description
        }
    }
}

ext {
    soktVersion = '0.2.0'
    junitVersion = '4.12'
    assertjVersion = '3.12.2'
    systemRulesVersion = '1.19.0'
}

repositories {
    mavenCentral()
    maven {
        url 'https://dl.bintray.com/ethereum/maven'
    }
    maven { 
        url 'https://dl.bintray.com/hotkeytlt/maven'
    }
}

dependencies {
    implementation "org.web3j:web3j-sokt:$soktVersion"
    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.assertj:assertj-core:$assertjVersion"
    testImplementation "com.github.stefanbirkner:system-rules:$systemRulesVersion"
}

spotless {
    groovy {
        excludeJava() // excludes all Java sources within the Groovy source dirs from formatting
        paddedCell() // Avoid cyclic ambiguities
        licenseHeaderFile "$rootDir/gradle/spotless/java.license"
    }
}
