buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
    }
}

plugins {
    id 'idea'
    id 'maven'
    id 'groovy'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.10.1'
    id "com.diffplug.gradle.spotless" version '3.25.0'
    id 'io.codearte.nexus-staging' version '0.21.1'
    id "de.marcphilipp.nexus-publish" version '0.4.0'
    id "de.undercouch.download" version '4.0.0'
}

apply {
    [
            'bintray',
            'jacoco',
            'java',
            'javadoc',
            'repositories',
            'publish',
            'spotless'
    ].each { buildScript ->
        download {
            src "https://raw.githubusercontent.com/web3j/build-tools/master/gradle/$buildScript/build.gradle"
            dest "$rootDir/gradle/$buildScript/build.gradle"
            overwrite true
            quiet true
            onlyIfModified true
        }
        from("$rootDir/gradle/$buildScript/build.gradle")
    }
}

description = 'Solidity Gradle Plugin'

gradlePlugin {
    plugins {
        solidity {
            id = 'org.web3j.solidity'
            implementationClass = 'org.web3j.solidity.gradle.plugin.SolidityPlugin'
        }
    }
}

pluginBundle {
    website = 'https://web3j.io/'
    vcsUrl = 'https://github.com/web3j/solidity-gradle-plugin'
    description = 'Gradle plugin providing tasks to compile Solidity contracts.'
    tags = ['solidity', 'ethereum']
    plugins {
        solidity {
            // id is captured from java-gradle-plugin configuration
            displayName = project.description
        }
    }
}

ext {
    junitVersion = '4.12'
    solcJVersion = '0.4.25'
    assertjVersion = '3.12.2'
    systemRulesVersion = '1.19.0'
}

repositories {
    mavenCentral()
    maven {
        url 'https://dl.bintray.com/ethereum/maven'
    }
}

dependencies {
    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.ethereum:solcJ-all:$solcJVersion"
    testImplementation "org.assertj:assertj-core:$assertjVersion"
    testImplementation "com.github.stefanbirkner:system-rules:$systemRulesVersion"
}

publishing {
    publications {
        maven(MavenPublication) {
            pom {
                developers {
                    developer {
                        id = 'conor10'
                        name = 'Conor Svensson'
                        email = 'conor@web3labs.com'
                        organization = 'Web3 Labs'
                    }
                    developer {
                        id = 'xaviarias'
                        name = 'Xavier Arias'
                        email = 'xavier@web3labs.com'
                        organization = 'Web3 Labs'
                    }
                }
            } 
        }
    }
}

bintrayUpload {
    enabled = false
}
